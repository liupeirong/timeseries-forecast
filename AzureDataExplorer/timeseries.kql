.create table nycdemand_daily (dt: datetime, energy: real) 
.ingest into table nycdemand_daily ('https://your_account.blob.core.windows.net/your_container/path/to/nycdemand_daily.csv?your_SAS_token')
nycdemand_daily | summarize count(), max(dt), min(dt)

// time series decomposition
let lookback = 730; // 2 years
let horizon = 28; // 4 weeks
let test_begin = datetime(2017-07-10);
let test_end = datetime_add('day', horizon-1, test_begin);
let train_begin = datetime_add('day', 0-lookback, test_begin);
let train_end = datetime_add('day', -1, test_begin);
let delta = 1d;
nycdemand_daily
| make-series energy=sum(energy) on dt from train_begin to train_end step delta
| extend(baseline, seasonal, trend, residual) = series_decompose(energy, -1, 'linefit') //baseline = seasonal + trend
| render timechart with(title='decomposition', ysplit = panels) 

// detect anomaly
let lookback = 730; // 2 years
let horizon = 28; // 4 weeks
let test_begin = datetime(2017-07-10);
let test_end = datetime_add('day', horizon-1, test_begin);
let train_begin = datetime_add('day', 0-lookback, test_begin);
let train_end = datetime_add('day', -1, test_begin);
let delta = 1d;
nycdemand_daily
| make-series energy=sum(energy) on dt from train_begin to train_end step delta 
| extend (anomalies, score, baseline) = series_decompose_anomalies(energy, 1.5, -1, 'linefit')
| render anomalychart with(anomalycolumns=anomalies, title='anomalies') 

// forecast daily for 28 days
let lookback = 730; // 2 years
let horizon = 28; // 4 weeks
let test_begin = datetime(2017-07-10);
let test_end = datetime_add('day', horizon-1, test_begin);
let train_begin = datetime_add('day', 0-lookback, test_begin);
let train_end = datetime_add('day', -1, test_begin);
let delta = 1d;
nycdemand_daily
| where dt < test_begin
| make-series energy=sum(energy) on dt from train_begin to test_end step delta // or to test_end + horizon * delta to predict the future instead of test
| extend forecast = series_decompose_forecast(energy, horizon)
| render timechart with(title='forecast')

// calculate MAPE
let lookback = 730; // 2 years
let horizon = 28; // 4 weeks
let test_begin = datetime(2017-07-10);
let test_end = datetime_add('day', horizon-1, test_begin);
let train_begin = datetime_add('day', 0-lookback, test_begin);
let train_end = datetime_add('day', -1, test_begin);
let delta = 1d;
nycdemand_daily
| make-series energy=sum(energy) on dt in range(train_begin, test_end, delta)
| extend forecast = series_decompose_forecast(energy, horizon)
| mv-expand dt, energy, forecast
| where dt > train_end
| extend ape = 100 * abs(todouble(energy) - todouble(forecast))/todouble(energy)
| summarize avg(ape)
